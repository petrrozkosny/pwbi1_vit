01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________

07 pocet destivych dnu ruzyne = 
-- v danem roce, lokalite ruzyne, kdy srazky >0
COUNTX(
    FILTER(data,
    data[lokalita] = "RUZYNE"
    && data[srazky] >0),
data[datum])

__________________________________

cv03 minimalni datum ruzyne = 
MINX(FILTER(data,data[lokalita]="RUZYNE"),data[datum])

______________________________________________

cv04 minimalni datum rok bez ohledu na mesic = 
MINX(FILTER(ALL(data),
data[rok] = MAX(data[rok])),
data[datum])

________________________________________

cv05 minimalni datum predchozi rok = 
-- v danem mesici
MINX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok]) - 1
    && data[mesic] = MAX(data[mesic])),
data[datum])

_______________________________________

08 maximalni maximalni teplota lokalita = 

-- maximum ze sloupce maximalni_teplota napric roky, ve vybrane lokalite
IF(MAX(data[maximalni_teplota]) = 
MAXX(
    FILTER(
        ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])),
data[maximalni_teplota]),"TOMATO")

__________________________________________


09 vybrana lokalita = 

"Vyvoj maximalnich teplot v lokalite "&SELECTEDVALUE(data[lokalita])

__________________________________________

10 maximalni teplota v roce napric lokalitami = 

MAXX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok])),
data[maximalni_teplota])

_____________________________________

cv08 suma srazek kumulativne = 

SUMX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok])
    && data[mesic] <= SELECTEDVALUE(data[mesic])),
data[srazky])

